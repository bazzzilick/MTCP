import string;
import math/math;
import ds/tree;
import lingo/pegcode/driver;

arithGrammarOp : ref Maybe<[PegOp]> = ref None();

initGrammar() {
	onlyOnce(arithGrammarOp, \ -> {
		compilePegGrammar("#include sandbox/lab7/lab7lingo.lingo");
	});
}

Rational(num: int, den: int);

mulRationals(l: Rational, r: Rational) -> Rational {
    //nod
    cdiv = gcd(l.num * r.num, l.den * r.den);
    Rational((l.num * r.num)/cdiv, (l.den * r.den)/cdiv);
}

divRationals(l: Rational, r: Rational) -> Rational {
    cdiv = gcd(l.num * r.den, l.den * r.num);
    Rational((l.num * r.den)/cdiv, (l.den * r.num)/cdiv);
}

sumRationals(l: Rational, r: Rational) -> Rational {
    if (l.den == r.den) {
        Rational(l.num + r.num, l.den);
    } else {
        cdiv = gcd(l.num * r.den + r.num * l.den, l.den * r.den);
        Rational((l.num * r.den + r.num * l.den)/cdiv, (l.den * r.den)/cdiv);
    }
}

subRationals(l: Rational, r: Rational) -> Rational {
    if (l.den == r.den) {
        Rational(l.num - r.num, l.den);
    } else {
        cdiv = gcd(l.num * r.den - r.num * l.den, l.den * r.den);
        Rational((l.num * r.den - r.num * l.den)/cdiv, (l.den * r.den)/cdiv);
    }
}

negRational(r: Rational) -> Rational {
    Rational(-r.num, r.den);
}

r2s(e: Rational) -> string {
    if (e.den == 1) {
        i2s(e.num);
    } else {
        concatStrings([i2s(e.num), "/", i2s(e.den)]);
    }
}


Expr ::= Var, IntConst, RatConst, Neg, Div, Sub, Mul, Sum;
Var : (name : string);
IntConst : (value : int);
RatConst : (value : Rational);
Neg : (e : Expr);
Div : (left : Expr, right : Expr);
Sub : (left: Expr, right : Expr);
Mul : (left : Expr, right : Expr);
Sum : (left : Expr, right : Expr);

mulSimplify(l : Expr, r : Expr) -> Expr {
	if (l == IntConst(0) || r == IntConst(0)) {IntConst(0)} 
    else 
		if (l == IntConst(1)) {r}
    	else 
			if (r == IntConst(1)) {l} 
            else {Mul(l, r)}
}

divSimplify(l : Expr, r : Expr) -> Expr {
	if (l == r) {IntConst(1)}
    else 
		if (r == IntConst(1)) {l} 
        else {Div(l, r)}
}

sumSimplify(l : Expr, r : Expr) -> Expr {
	if (l == IntConst(0) && r == IntConst(0)) {IntConst(0)} 
    else 
		if (l == IntConst(0)) {r}
    	else 
			if (r == IntConst(0)) {l}
    		else {Sum(l, r)}
}

subSimplify(l : Expr, r : Expr) -> Expr {
	if (l == IntConst(0) && r == IntConst(0)) {IntConst(0)} 
    else 
		if (l == r) {IntConst(0)}
        else 
			if (r == IntConst(0)) {l}
            else 
				if (l == IntConst(0)) {Neg(r)}
            	else {Sub(l, r)}
}

simplify(e : Expr) -> Expr {
    
    switch(e) {
		Var(x): Var(x);
		IntConst(c): IntConst(c);
		RatConst(c): RatConst(c);
		Neg(r): Neg(simplify(r));
		Div(l, r) : {
			leftSimple = simplify(l);
			rightSimple = simplify(r);
			divSimplify(leftSimple, rightSimple);
		}
		Sub(l, r) : {
			leftSimple = simplify(l);
			rightSimple = simplify(r);
			subSimplify(leftSimple, rightSimple);
		}
		Mul(l, r) : {
			leftSimple = simplify(l);
			rightSimple = simplify(r);
			mulSimplify(leftSimple, rightSimple);
		}
		Sum(l, r) : {
			leftSimple = simplify(l);
			rightSimple = simplify(r);
			sumSimplify(leftSimple, rightSimple);
		}
    }
}


calcAST(exp : Expr, vars : Tree<string, Rational>) -> Maybe<Rational> {

	switch(exp) {
		Var(x) : {
	
			switch(lookupTree(vars, x)) {
				Some(val): Some(val);
				None(): {
					println("variable is not initialized");
					None();
				};
			}
		}
		IntConst(c) : {
			Some(Rational(c, 1));
		}
		RatConst(c) : {
			Some(c);
		}
		Neg(e) : {
		
			switch(calcAST(e, vars)) {
				Some(expr): Some(negRational(expr));
				None(): None();
			};
		}
		Div(l, r) : {
			
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(divRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
		Sub(l, r) : {
		
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(subRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
		Mul(l, r) : {
		
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(mulRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
		Sum(l, r) : {
			
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(sumRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
	}
}

calc(str : string, vars : Tree<string, Rational>) -> Maybe<Rational> {
	exp = simplify(parsic(initGrammar(), str, defaultPegActions));
    println("simplified expr:");
	println(exp);
    calcAST(exp, vars);
}

main() {
	
	vars1 : Tree<string, Rational> = setTree(makeTree(), "x", Rational(1, 2));
	vars2 = setTree(vars1, "y", Rational(2, 1));
	vars3 = setTree(vars2, "z", Rational(2, 5));

	//str = "((x + y) * (z + 1))";
    // (((1/2) + (2/1)) * ((2/5) + 1)) = ((5/2) * (7/5)) = 7/2

    str = "((x + y) * (z + ((1 - 0) * (1 / 1))))";
    // (((1/2) + (2)) * ((2/5) + 1)) == ((5/2) * (7/5)) == 7/2

	println("var:");
	println(tree2pairs(vars3));
	println("exp:");
	println(str);

	switch(calc(str, vars3)) {
		Some(val): {
			println("res:");
			println(r2s(val));
		};
		None(): println("err");
	}
}