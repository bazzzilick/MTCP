import string;
import math/math;
import ds/tree;
import lingo/pegcode/driver;

arithGrammarOp : ref Maybe<[PegOp]> = ref None();

initGrammar() {
	onlyOnce(arithGrammarOp, \ -> {
		compilePegGrammar("#include sandbox/lab7/lab7lingo.lingo");
	});
}

Rational(num: int, den: int);

mulRationals(l: Rational, r: Rational) -> Rational {
    //nod
    cdiv = gcd(l.num * r.num, l.den * r.den);
    Rational((l.num * r.num)/cdiv, (l.den * r.den)/cdiv);
}

divRationals(l: Rational, r: Rational) -> Rational {
    cdiv = gcd(l.num * r.den, l.den * r.num);
    Rational((l.num * r.den)/cdiv, (l.den * r.num)/cdiv);
}

sumRationals(l: Rational, r: Rational) -> Rational {
    if (l.den == r.den) {
        Rational(l.num + r.num, l.den);
    } else {
        cdiv = gcd(l.num * r.den + r.num * l.den, l.den * r.den);
        Rational((l.num * r.den + r.num * l.den)/cdiv, (l.den * r.den)/cdiv);
    }
}

subRationals(l: Rational, r: Rational) -> Rational {
    if (l.den == r.den) {
        Rational(l.num - r.num, l.den);
    } else {
        cdiv = gcd(l.num * r.den - r.num * l.den, l.den * r.den);
        Rational((l.num * r.den - r.num * l.den)/cdiv, (l.den * r.den)/cdiv);
    }
}

negRational(r: Rational) -> Rational {
    Rational(-r.num, r.den);
}

r2s(e: Rational) -> string {
    if (e.den == 1) {
        i2s(e.num);
    } else {
        concatStrings([i2s(e.num), "/", i2s(e.den)]);
    }
}


Expr ::= Var, IntConst, RatConst, Neg, Div, Sub, Mul, Sum;
Var : (name : string);
IntConst : (value : int);
RatConst : (value : Rational);
Neg : (e : Expr);
Div : (left : Expr, right : Expr);
Sub : (left: Expr, right : Expr);
Mul : (left : Expr, right : Expr);
Sum : (left : Expr, right : Expr);


calcAST(exp : Expr, vars : Tree<string, Rational>) -> Maybe<Rational> {

	switch(exp) {
		Var(x) : {
	
			switch(lookupTree(vars, x)) {
				Some(val): Some(val);
				None(): {
					println("variable is not initialized");
					None();
				};
			}
		}
		IntConst(c) : {
			Some(Rational(c, 1));
		}
		RatConst(c) : {
			Some(c);
		}
		Neg(e) : {
		
			switch(calcAST(e, vars)) {
				Some(expr): Some(negRational(expr));
				None(): None();
			};
		}
		Div(l, r) : {
			
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(divRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
		Sub(l, r) : {
		
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(subRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
		Mul(l, r) : {
		
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(mulRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
		Sum(l, r) : {
			
			switch(calcAST(l, vars)) {
                Some(left): {
					switch(calcAST(r, vars)) {
                    	Some(right): Some(sumRationals(left, right));
                    	None(): None();
					}
            	};
                None(): None();
            };
		}
	}
}

calc(str : string, vars : Tree<string, Rational>) -> Maybe<Rational> {
	calcAST(parsic(initGrammar(), str, defaultPegActions), vars);
}

main() {
	
	vars1 : Tree<string, Rational> = setTree(makeTree(), "x", Rational(1, 2));
	vars2 = setTree(vars1, "y", Rational(2, 1));
	vars3 = setTree(vars2, "z", Rational(2, 5));
	
    // (((1/2) + (2/1)) * ((2/5) + 1)) = ((5/2) * (7/5)) = 7/2
	str = "((x + y) * (z + 1))";

	switch(calc(str, vars3)) {
		Some(val): {
			println("exp:");
			println(str);
			println("var:");
			println(tree2pairs(vars3));
			println("res:");
			println(r2s(val));
		};
		None(): println("err");
	}
}